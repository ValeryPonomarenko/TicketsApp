apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion app.compileSdkVersion
    defaultConfig {
        applicationId "me.vponomarenko.ticketsapp"
        minSdkVersion app.minSdkVersion
        targetSdkVersion app.compileSdkVersion
        versionCode getVersionCode(app.versionName)
        versionName app.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "api"
    productFlavors {
        real {
            dimension "api"
        }
        mock {
            dimension "api"
        }
    }
    variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation appModules.presentation.search
    implementation appModules.presentation.details
    implementation appModules.domain.search
    implementation appModules.domain.details
    implementation appModules.data

    implementation deps.kotlin
    implementation deps.appcompat
    implementation deps.material
    implementation deps.dagger
    implementation deps.componentsmanager
    kapt annotation.dagger

    testImplementation testing.junit
    androidTestImplementation testing.runner
    androidTestImplementation testing.espresso_core
}

int getVersionCode(versionName) {
    def (major, minor, patch) = versionName.toLowerCase().tokenize('.')

    (major, minor, patch) = [major, minor, patch].collect{it.toInteger()}

    (major * 1000000) + (minor * 1000) + (patch * 10)
}